#lang staapl/pic18 \ -*- forth -*-

\ PIC DIP28 MINI KIT - PIC Developmenâ€‹t Board kit for DIP28 PIC with 18F2550 + 4Mhz XTAL
\ Connected to 4 Relay Module board


provide-all

\ config
#x300000 org-begin

\ no extended instruction set, no wdt, no code protect, 

#x20 , #x0E , #x3A , #x1E ,   \ 4MHz XT drives PLL directly
#x00 , #x81 , #x85 , #x00 ,   \ extended instruction set disabled
#x0F , #xC0 , #x0F , #xE0 ,
#x0F , #x40 , 

org-end

staapl pic18/boot   \ chip init code

load p18f2550.f          \ chip macros
load monitor-usbserial.f \ boot block + interactive monitor over USB ACM serial port

load usb.f                     \ USB library
require-file 4-relay-module-desc.rkt \ scheme file with descriptor tables       
: device-descriptor        'scheme device  usb-descriptor
: configuration-descriptor 'scheme configs usb-descriptors
: string-descriptor        'scheme strings usb-descriptors

staapl pic18/string
staapl pic18/afregs
staapl pic18/vector

load debug.f


macro
: live-module ` staapl/pic18/live ;
: fosc 48000000 ;
: monitor-EP 1 ;
: total-EP 4 ;  \ control:0, ACM data:1, ACM control:2, MIDI:3
: service-lo ;  \ inlined in USB kernel lo-pri isr



    
: init-board  \ Turn everything off before enabling outputs
    \ A: low nibble is LED1-LED4 active low
    \ B: low nibble relay active high
    
    #x0F LATA !
    0 LATB !
    0 LATC !
    
    #xF0 TRISA !
    0 TRISB !
    0 TRISC !

    \ Blink LEDs on startup
    hello
    ;

: application interpreter ;    
forth

: pause 0 begin 0 begin 1+ c? until drop 1+ c? until drop ;
: hello 0 LATA ! pause #xF LATA ! ;  
: relay! #xF and LATB ! ;
: 5pause 5 for pause next ;
: diag      0 16 for dup            relay! 1 + 5pause next drop 0 relay! ;   
: diag-grey 0 16 for dup dup >> xor relay! 1 + 5pause next drop 0 relay! ;     
  

\ Welcome to Racket v6.0.1.
\ > (require staapl/live/c-console)
\ > (require "4-relay-module.fm")
\ > (struct-init relay!)
\ #define WORDS {{.name="relay!",.addr=1267},}
